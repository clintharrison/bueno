"""the kindle monorepo"""

module(name = "bueno")

bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "bazel_skylib", version = "1.8.1")
bazel_dep(name = "rules_cc", version = "0.2.8")
bazel_dep(name = "gcc_toolchain", version = "0.9.0")
bazel_dep(name = "rules_go", version = "0.57.0")
bazel_dep(name = "gazelle", version = "0.45.0")

go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.from_file(go_mod = "//:go.mod")
go_sdk.nogo(nogo = "//:nogo")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(
    go_deps,
    "com_github_godbus_dbus_v5",
    "com_github_google_uuid",
    "com_github_holoplot_go_evdev",
    "com_github_lmittmann_tint",
    "com_github_pilebones_go_udev",
    "in_gopkg_yaml_v3",
)

git_override(
    module_name = "gcc_toolchain",
    commit = "60704890808323e0d44c199ad6bf56b6509ded46",
    remote = "https://github.com/clintharrison/gcc-toolchain",
)

gcc_toolchains = use_extension("@gcc_toolchain//toolchain:module_extensions.bzl", "gcc_toolchains", dev_dependency = True)
gcc_toolchains.toolchain(
    name = "gcc_toolchain_kindlehf",
    binary_prefix = "arm-kindlehf-linux-gnueabihf-",
    enable_fortran = False,
    gcc_version = "14.2.0",
    # No json.encode() in a MODULE.bazel file :/
    gcc_versions = '''\
    {
        "14.2.0": {
            "armv7": {
                "url": "https://github.com/Sighery/kindle-sdk/releases/download/build-20250905/scribe1-5.17.3.tar.gz",
                "sha256": "faefc807c9fc422019912a3153f3d1f1cc24bff7820bf0732eb8c6304038565d",
                "strip_prefix": "x-tools/arm-kindlehf-linux-gnueabihf/"
            }
        }
    }''',
    include_prefix = "arm-kindlehf-linux-gnueabihf/",
    symlink_sysroot_path = "arm-kindlehf-linux-gnueabihf/sysroot",
    target_arch = "armv7",
)
use_repo(gcc_toolchains, "gcc_toolchain_kindlehf")

register_toolchains(
    "@gcc_toolchain_kindlehf//:cc_toolchain",
    dev_dependency = True,
)

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "ace",
    build_file = "//third_party:ace.BUILD.bazel",
    integrity = "sha256-xRVX4w+JVfnE7Xr1LUBm+blT9LeSQHgwrZDXx8rg+4c=",
    patch_cmds = [
        # Code expects to include "ace/xyz.h" but we just have "xyz.h".
        # I think this is just a quirk from Marek's repo?
        """
        mkdir -p ace
        mv *.h ace/
        """,
    ],
    strip_prefix = "ace_headers-017b324e68dd236f85d73d14d273380d3424a016",
    url = "https://github.com/clintharrison/ace_headers/archive/017b324e68dd236f85d73d14d273380d3424a016.tar.gz",
)
